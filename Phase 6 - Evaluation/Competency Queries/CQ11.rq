PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX e: <http://knowdive.disi.unitn.it/etype#>



SELECT DISTINCT
	?poiName
    ?poiType
	(?poiDistance AS ?distanceMeters)
WHERE {
    VALUES ?searchType {
        "camposportivo"
        "piscina"
        "palestra"
        "impiantosciistico"
        "Campo sportivo (calcio, volley, basket, ...)"
        "Stadio del ghiaccio"
        "scuola sportiva"
        "piste da sci"
        "Palestra"
        "Piscina"
        "stadiosalto"
        "impiantosci"
    }
    
    ################################################################
    # 1) Get the user's most recent lat/long before ?departTime
    ################################################################
    {
        SELECT
            (?locTimestamp AS ?locTime)
            (?lat AS ?uLat)
            (?long AS ?uLong)
        WHERE {
            BIND("2018-05-15T15:00:00"^^xsd:dateTime AS ?deptime)
            BIND(<http://localhost:8080/source/student_GID-53021_1> AS ?usr)
            ?usr e:has_located_GID-85982 ?userLoc .
            ?userLoc
                e:has_latitude_GID-45424 ?lat ;
                e:has_longitude_GID-45429 ?long ;
                e:has_timestamp_GID-27373 ?locTimestamp .
                
            FILTER(?locTimestamp < ?deptime)
        }
        ORDER BY DESC(?locTimestamp)
        LIMIT 1
    }

    ################################################################
    # 2) Get the POI lat/long filtered by type
    ################################################################
    ?poi a e:point_of_interest_KGE24-0A-20;
        e:has_PoI_name_KGE24-0A-8 ?poiName;
        e:has_PoI_type_KGE24-0A-7 ?poiType;
        e:has_latitude_GID-45424 ?poiLat ;
        e:has_longitude_GID-45429 ?poiLong .
    
    FILTER(LCASE(?poiType) = LCASE(?searchType))
        
    ################################################################
    # 3) Calculate distance using GeoSPARQL
    ################################################################
    BIND(STRDT(CONCAT("POINT(", STR(?uLong), " ", STR(?uLat), ")"), geo:wktLiteral) AS ?userPoint)
    BIND(STRDT(CONCAT("POINT(", STR(?poiLong), " ", STR(?poiLat), ")"), geo:wktLiteral) AS ?poiPoint)
    BIND(geof:distance(?userPoint, ?poiPoint, <http://www.opengis.net/def/uom/OGC/1.0/metre>) AS ?poiDistance)
}
ORDER BY ?poiDistance