PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX e: <http://knowdive.disi.unitn.it/etype#>

# What was the studentâ€™s mood when they were at the Coop supermarket?

SELECT ?poi ?poiName (AVG(?moodValue) AS ?moodInCoop)
WHERE {
    # GET THE POI WE WANT
    {
        SELECT ?poi ?poiName
        WHERE{
            VALUES ?types { "coop" }
            ?poi a e:point_of_interest_KGE24-0A-20;
            	e:has_PoI_name_KGE24-0A-8 ?poiName.
            FILTER(CONTAINS(LCASE(?poiName), LCASE(?types)))
        } 
    }
    {
        SELECT ?stay
        WHERE{
            BIND(<http://localhost:8080/source/student_GID-53021_44> AS ?user)
            ?user e:has_stay_put_GID-101763 ?stay.
        }
    } 
    #                        
    ?stay e:has_inside_GID-106969 ?poi;
        e:has_timestamp_GID-27373 ?stayStartTime;
        e:has_duration_GID-72859 ?stayDuration .
        BIND(?stayStartTime + ?stayDuration AS ?stayEndTime)
    # GET THE MOODS WE WANT
    {
        SELECT ?mood ?moodValue ?moodStartTime ?moodEndTime
        WHERE {
            BIND(<http://localhost:8080/source/student_GID-53021_55> AS ?user)
            ?user e:has_feel_GID-101373 ?mood.
            ?mood e:has_mood_GID-39957 ?moodVal ;
                e:has_timestamp_GID-27373 ?moodStartTime ;
                e:has_duration_GID-72859 ?moodDuration .
            BIND(xsd:float(?moodVal) AS ?moodValue)
            BIND(?moodStartTime + ?moodDuration AS ?moodEndTime)
            FILTER(?moodValue > 0)
        }
    }
    FILTER(
        ((?moodStartTime <= ?stayEndTime) && (?moodEndTime >= ?stayStartTime))
    )
} 
GROUP BY ?poi ?poiName
